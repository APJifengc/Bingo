plugins {
    id 'java'
    id 'maven-publish'
    id 'io.freefair.lombok' version '6.0.0-m2'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'io.apjifengc.bingo'
version '2.0.0'

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.onarandombox.com/content/groups/public/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://maven.enginehub.org/repo/' }
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT'
    implementation 'com.mojang:authlib:2.3.31'
    implementation 'com.sk89q.worldedit:worldedit-core:7.2.7'
    implementation 'com.sk89q.worldedit:worldedit-bukkit:7.2.7'
    shadow 'io.github.bananapuncher714:nbteditor:7.17.0'

    implementation 'org.jetbrains:annotations:22.0.0'
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = ''
}

build {
    dependsOn(shadowJar)
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
    destinationDirectory = file("$rootDir/run/plugins/")
}


processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml' expand 'version': project.version
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url = project.properties.repo_address
            credentials {
                username project.properties.repo_username as String
                password project.properties.repo_password as String
            }
        }
    }
}
